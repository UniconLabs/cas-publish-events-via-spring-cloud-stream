apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply from: 'http://dl.bintray.com/scalding/generic/waroverlay.gradle'
apply from: 'https://raw.githubusercontent.com/apereo/cas/master/gradle/overrides.gradle'

bootRepackage {
    enabled = false
}

springBoot  {
    mainClass = "org.springframework.boot.loader.WarLauncher"
}

bootRun {
    addResources = true
}

repositories {
    jcenter()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-stream-dependencies:Chelsea.SR1'
    }
}

dependencies {
    compile "org.apereo.cas:cas-server-webapp:${project.'cas.version'}@war"
    compile "org.apereo.cas:cas-server-support-json-service-registry:${project.'cas.version'}"

    //For development
    compile "org.apereo.cas:cas-server-core-api-events:${project.'cas.version'}"
    compile "org.apereo.cas:cas-server-core:${project.'cas.version'}"
    compile "org.springframework:spring-context"

    compile 'org.springframework.cloud:spring-cloud-stream'
    compile 'org.springframework.cloud:spring-cloud-starter-stream-rabbit'
}

war {
    baseName 'cas'
    includeWarJars = true
    entryCompression = ZipEntryCompression.STORED
    manifest {
        from manifestFile()
    }
}

task explodeWar(type: Copy) {
    group = "build"
    description = "Explode the cas.war"
    from zipTree(project.war.outputs.files.singleFile)
    into "${buildDir}/cas"
}

File manifestFile() {
    def warfile = configurations.runtime.asFileTree.matching {
        include '**/*.war'
    }
    def manifest = zipTree(warfile.singleFile).matching {
        include '**/*.MF'
    }
    def dst = new File("${project.rootDir}/etc/cas/MANIFEST.MF")
    dst.delete()
    dst << manifest.singleFile.text
    return dst
}
